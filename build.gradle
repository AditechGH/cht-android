buildscript {
	repositories {
		mavenCentral()
		maven { url "https://jitpack.io" }
		jcenter()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:2.1.0'
		classpath 'com.github.alxndrsn:android-check:1f87548c0686d823e5f1490a2053b70bd9f5022e'
		classpath 'com.trickyandroid:jacoco-everywhere:0.2.1'
	}
	configurations.all {
		resolutionStrategy {
			// use old jacoco so Robolectric test coverage is included in reports
			// track https://github.com/paveldudka/JacocoEverywhere/issues/14 for a less hacky solution
			force 'org.jacoco:org.jacoco.core:0.7.2.201409121644'
		}
	}
}
apply plugin: 'com.android.application'
apply plugin: 'com.noveogroup.android.check'
apply plugin: 'jacoco-everywhere'

// enable verbose lint warnings
gradle.projectsEvaluated {
	tasks.withType(JavaCompile) {
		options.compilerArgs << '-Xlint:deprecation'
	}
}

repositories {
	mavenCentral()
	maven { url 'https://download.01.org/crosswalk/releases/crosswalk/android/maven2' }
}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])
	compile 'com.android.support:appcompat-v7:21.0.3'
	compile 'org.xwalk:xwalk_core_library:23.53.589.4:arm@aar'
}

def isReleaseBuild = {
	return System.env.JENKINS == 'true' || (System.env.TRAVIS == 'true' &&
			System.env.TRAVIS_BRANCH == 'master')
}
def getVersionCode = {
	if(isReleaseBuild()) {
		def code = System.env.TRAVIS == 'true' ?
				System.env.TRAVIS_BUILD_NUMBER :
				System.env.VERSION_TO_BUILD.split(/\./)[-1]
		return Integer.parseInt(code)
	}
	return 1
}
def getVersionName = {
	if(isReleaseBuild()) {
		if(System.env.TRAVIS == 'true') {
			def v = System.env.MEDIC_VERSION_NAME
			if(!v) throw new RuntimeException(
					'Missing required env var: MEDIC_VERSION_NAME')
			return v
		} else return System.env.VERSION_TO_BUILD
	}
	return 'SNAPSHOT'
}

connectedCheck.doLast {
	// Add CLI report for Jacoco coverage
	def slurper = new XmlSlurper()
	slurper.setFeature("http://apache.org/xml/features/disallow-doctype-decl", false) 
	slurper.setFeature("http://apache.org/xml/features/nonvalidating/load-external-dtd", false);

	['generic', 'medic'].each { flavour ->
		def report = slurper.parse("./build/reports/coverage/${flavour}/debug/report.xml")

		println "------ Code Coverage (flavour: $flavour)) ------"
		report.package.each { p ->
			println "--> ${p.@name.toString().replaceAll('/', '.')}"
			p.counter.each { c ->
				def covered = c.@covered.toString() as int
				def missed = c.@missed.toString() as int
				def percent = ((100 * covered) / (covered + missed))
				println String.format('    %-16s %.1f ', c.@type, percent) + '%'
			}
			println "----------------------------------"
		}
	}
}

android {
	compileSdkVersion 22
	buildToolsVersion '22.0.1'
	packagingOptions {
		exclude 'META-INF/LICENSE'
		exclude 'META-INF/NOTICE'
	}

	defaultConfig {
		versionCode getVersionCode()
		versionName getVersionName()
		archivesBaseName = "${project.name}-${versionName}"
	}

	applicationVariants.all {
		buildConfigField "boolean", "DISABLE_APP_URL_VALIDATION", "Boolean.parseBoolean(\"${System.env.DISABLE_APP_URL_VALIDATION}\")";
		buildConfigField "String", "LOG_TAG", '"MedicMobile"'
	}

	signingConfigs {
		release {
			storeFile file(System.env.ANDROID_KEYSTORE_PATH ?: signingConfigs.debug.storeFile)
			storePassword System.env.ANDROID_KEYSTORE_PASSWORD ?: signingConfigs.debug.storePassword
			keyAlias System.env.ANDROID_KEY_ALIAS ?: signingConfigs.debug.keyAlias
			keyPassword System.env.ANDROID_KEY_PASSWORD ?: signingConfigs.debug.keyPassword
		}
	}

	buildTypes {
		debug {
			testCoverageEnabled = true
		}
		release {
			minifyEnabled false
			proguardFile getDefaultProguardFile('proguard-android.txt')
			signingConfig signingConfigs.release
		}
	}

	check {
		abortOnError true
	}

	lintOptions {
		disable 'UnusedResources' // linter can't handle static imports, so just skip this test
		disable 'GradleDependency' // TODO update to latest support-v4 lib and re-enable this rule

		warningsAsErrors true

		xmlReport false

		if(System.env.TRAVIS == 'true') {
			abortOnError true
			htmlReport false
			textReport true
			textOutput 'stdout'
		}
	}

	productFlavors {
		unbranded {
			// we will not create project-specific src directories
			// for `unbranded` - it will use the defaults in
			// src/main
		}
		medicmobiledemo {
			applicationId = 'org.medicmobile.webapp.mobile.medicmobiledemo'
		}
		livinggoods {
			applicationId = 'org.medicmobile.webapp.mobile.livinggoods'
		}
		bracuganda {
			applicationId = 'org.medicmobile.webapp.mobile.bracuganda'
		}
		livinggoodskenya {
			applicationId = 'org.medicmobile.webapp.mobile.livinggoodskenya'
		}
		musomali {
			applicationId = 'org.medicmobile.webapp.mobile.musomali'
		}
	}
}
